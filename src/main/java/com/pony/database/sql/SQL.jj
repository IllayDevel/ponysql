/*
 * Pony SQL Database ( http://i-devel.ru )
 * Copyright (C) 2019-2020 IllayDevel.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

options {
  STATIC = false;
  UNICODE_INPUT = true;
  OPTIMIZE_TOKEN_MANAGER = true;
//  IGNORE_CASE = true;
  DEBUG_PARSER = false;
}

PARSER_BEGIN(SQL)

package com.pony.database.sql;

import com.pony.database.TType;
import com.pony.database.Assignment;
import com.pony.database.JoiningSet;
import com.pony.database.Expression;
import com.pony.database.Variable;
import com.pony.database.FunctionDef;
import com.pony.database.FunctionFactory;
import com.pony.database.Operator;
import com.pony.database.StatementTree;
import com.pony.database.ParameterSubstitution;
import com.pony.database.global.*;
import com.pony.database.interpret.*;
import com.pony.database.TObject;
import java.util.ArrayList;
import java.util.Stack;

public class SQL {

  // State variables for the parser,

  /**
   * Set to true if the SQL identifiers are converted to upper case.
   * NOTE: Purely experimental feature!
   */
  private boolean case_insensitive_identifiers = false;
  
  /**
   * The parameter id.
   */
  private int parameter_id = 0;
  
  
  /**
   * Resets the parameter id.  This MUST be called before a parser is used
   * to parse a statement.
   */
  public void reset() {
    parameter_id = 0;
  }
  
  /**
   * Creates and returns a parameter substitution.  This is called when the
   * parser comes across a '?' style object.  This object is used to mark an
   * expression with a place mark that can be substituted for a value later.
   */
  public ParameterSubstitution createSubstitution(String image) {
    ParameterSubstitution ps = new ParameterSubstitution(parameter_id);
    ++parameter_id;
    return ps;
  }
  
  
  /**
   * If the parser has been defined as case insensitive then this
   * returns the uppercase version of the given string.
   *
   * NOTE: This actually doesn't do anything because the case is now resolved
   *   outside the parser.
   */
  public String caseCheck(String identif) {
//    if (case_insensitive_identifiers) {
//      return identif.toUpperCase();
//    }
    return identif;
  }
  
  /**
   * Helper for expression parsing.
   * Called when an end parenthese has been found.
   */
  public void expEndParen(Expression exp, Stack stack) {
    Operator op = (Operator) stack.pop();
    while (!op.is("(")) {
      addOperatorToExpression(exp, op);
      op = (Operator) stack.pop();
    }
  }
  
  /**
   * Helper for expression parsing.
   * Called when an operator has been read in.  This needs to check precedence
   * and add the operator to the expression as appropriate.
   */
  public void expOperator(Expression exp, Stack stack, Operator op) {
    int precedence = op.precedence();
    flushOperatorStack(exp, stack, precedence);
    stack.push(op);
  }

  /**
   * Flush the operator stack until the stack is either empty or the top
   * element is either a "(" or of a precedence lower than the given
   * precedence.
   */
  public void flushOperatorStack(Expression exp, Stack stack, int precedence) {
    if (!stack.empty()) {
      Operator top_op = (Operator) stack.pop();
      while (!top_op.is("(") && top_op.precedence() >= precedence) {
        addOperatorToExpression(exp, top_op);
        if (stack.empty()) {
          return;
        }
        top_op = (Operator) stack.pop();
      }
      stack.push(top_op);
    }
  }
  
  /**
   * Helper for expression parsing.
   * Called when an entire expression has been read in.  We need to empty
   * the stack.
   */
  public void expEnd(Expression exp, Stack stack) {
    while (!stack.empty()) {
      Operator op = (Operator) stack.pop();
      addOperatorToExpression(exp, op);
    }
  }

  /**
   * Helper for expression parsing.
   * Adds an operator to the given expression.
   */
  public void addOperatorToExpression(Expression exp, Operator op) {
    if (op.is("not")) {
      exp.addElement(null);
    }
    exp.addOperator(op);
  }

  
  
  public static void main(String args[]) throws ParseException {
    SQL parser = new SQL(System.in);
    parser.Test();
  }

}

PARSER_END(SQL)


SKIP : {

  " "
| "\t"
| "\n"
| "\r"
|  <"//" (~["\n","\r"])* ("\n" | "\r" | "\r\n")>
|  <"--" (~["\n","\r"])* ("\n" | "\r" | "\r\n")>
//|  <"/*" (~["*"])* "*" ("*" | ~["*","/"] (~["*"])* "*")* "/">

}

TOKEN: {

  <STAR:       "*" >

| <ASSIGNMENT: "=" >
| <EQUALS:     "==" >
| <GR:         ">" >
| <LE:         "<" >
| <GREQ:       ">=" >
| <LEEQ:       "<=" >
| <NOTEQ:      "!=" | "<>" >
| <DIVIDE:     "/" >
| <ADD:        "+" >
| <SUBTRACT:   "-" >
| <CONCAT:     "||" >

}

TOKEN [IGNORE_CASE] : {
  
    <BOOLEAN_LITERAL: "true" | "false" >
  | <NULL_LITERAL:    "null" >
      // NOTE: Handling regex literals is a horrible horrible hack.  The <REGEX_LITERAL> 
      //   token starts with 'regex /' and the regex string follows.
      //   The reason for this hack is because / clashes with <DIVIDE>
  | <REGEX_LITERAL:   "regex /" (~["/","\n","\r"] | "\\/" )* "/" ( "i" | "s" | "m" )* >
  
}

TOKEN [IGNORE_CASE] : { /* KEYWORDS */

  <DROP:        "drop">
| <SHOW:        "show">
| <ALTER:       "alter">
| <SELECT:      "select">
| <UPDATE:      "update">
| <CREATE:      "create">
| <DELETE:      "delete">
| <INSERT:      "insert">
| <COMMIT:      "commit">
| <COMPACT:     "compact">
| <EXPLAIN:     "explain">
| <ROLLBACK:    "rollback">
| <OPTIMIZE:    "optimize">
| <DESCRIBE:    "describe">
| <SHUTDOWN:    "shutdown">

| <IS:          "is">
| <AS:          "as">
| <ON:          "on">
| <IF:          "if">
| <TO:          "to">
| <NO:          "no">
| <ALL:         "all">
| <ANY:         "any">
| <SET:         "set">
| <USE:         "use">
| <ASC:         "asc">
| <OLD:         "old">
| <NEW:         "new">
| <SQLADD:      "add">
| <FOR:         "for">
| <ROW:         "row">
| <EACH:        "each">
| <CALL:        "call">
| <BOTH:        "both">
| <SOME:        "some">
| <FROM:        "from">
| <LEFT:        "left">
| <DESC:        "desc">
| <INTO:        "into">
| <JOIN:        "join">
| <TRIM:        "trim">
| <VIEW:        "view">
| <LOCK:        "lock">
| <WITH:        "with">
| <USER:        "user">
| <CAST:        "cast">
| <LONG:        "long">
| <NAME:        "name">
| <JAVA:        "java">
| <AFTER:       "after">
| <START:       "start">
| <COUNT:       "count">
| <WHERE:       "where">
| <CYCLE:       "cycle">
| <CACHE:       "cache">
| <RIGHT:       "right">
| <TABLE:       "table">
| <LIMIT:       "limit">
| <INNER:       "inner">
| <INDEX:       "index">
| <CROSS:       "cross">
| <OUTER:       "outer">
| <CHECK:       "check">
| <USING:       "using">
| <UNION:       "union">
| <GRANT:       "grant">
| <USAGE:       "usage">
| <SQLRETURN:   "return">
| <BEFORE:      "before">
| <UNLOCK:      "unlock">
| <ACTION:      "action">
| <GROUPS:      "groups">
| <REVOKE:      "revoke">
| <OPTION:      "option">
| <PUBLIC:      "public">
| <EXCEPT:      "except">
| <IGNORE:      "ignore">
| <SCHEMA:      "schema">
| <EXISTS:      "exists">
| <VALUES:      "values">
| <HAVING:      "having">
| <UNIQUE:      "unique">
| <SQLCOLUMN:   "column">
| <RETURNS:     "returns">
| <ACCOUNT:     "account">
| <LEADING:     "leading">
| <NATURAL:     "natural">
| <BETWEEN:     "between">
| <TRIGGER:     "trigger">
| <SQLDEFAULT:  "default">
| <VARYING:     "varying">
| <EXECUTE:     "execute">
| <CALLBACK:    "callback">
| <MINVALUE:    "minvalue">
| <MAXVALUE:    "maxvalue">
| <FUNCTION:    "function">
| <SEQUENCE:    "sequence">
| <RESTRICT:    "restrict">
| <PASSWORD:    "password">
| <TRAILING:    "trailing">
//| <NOTNULL:     "not null">
| <GROUPBY:     "group by">
| <ORDERBY:     "order by">
| <DEFERRED:    "deferred">
| <DISTINCT:    "distinct">
| <LANGUAGE:    "language">
| <INCREMENT:   "increment">
| <PROCEDURE:   "procedure">
| <CHARACTER:   "character">
| <IMMEDIATE:   "immediate">
| <INITIALLY:   "initially">
| <TEMPORARY:   "temporary">
| <INTERSECT:   "intersect">
| <PRIVILEGES:  "privileges">
| <CONSTRAINT:  "constraint">
| <DEFERRABLE:  "deferrable">
| <REFERENCES:  "references">

| <PRIMARY:     "primary">
| <FOREIGN:     "foreign">
| <KEY:         "key">

| <INDEX_NONE:  "index_none">
| <INDEX_BLIST: "index_blist">

| <GROUPMAX:    "group max">

| <COLLATE:     "collate">

// Collate strengths,

| <PRIMARY_STRENGTH:   "primary_strength">
| <SECONDARY_STRENGTH: "secondary_strength">
| <TERTIARY_STRENGTH:  "tertiary_strength">
| <IDENTICAL_STRENGTH: "identical_strength">

// Collate decomposition levels,

| <NO_DECOMPOSITION:        "no_decomposition">
| <CANONICAL_DECOMPOSITION: "canonical_decomposition">
| <FULL_DECOMPOSITION:      "full_decomposition">

// Data types,

| <BIT:         "bit">
| <INT:         "int">
| <REAL:        "real">
| <CLOB:        "clob">
| <BLOB:        "blob">
| <CHAR:        "char">
| <TEXT:        "text">
| <DATE:        "date">
| <TIME:        "time">
| <FLOAT:       "float">
| <BIGINT:      "bigint">
| <DOUBLE:      "double">
| <STRING:      "string">
| <BINARY:      "binary">
| <NUMERIC:     "numeric">
| <DECIMAL:     "decimal">
| <BOOLEAN:     "boolean">
| <TINYINT:     "tinyint">
| <INTEGER:     "integer">
| <VARCHAR:     "varchar">
| <SMALLINT:    "smallint">
| <VARBINARY:   "varbinary">
| <TIMESTAMP:   "timestamp">
| <JAVA_OBJECT: "java_object">
| <LONGVARCHAR: "longvarchar">
| <LONGVARBINARY: "longvarbinary">

| <TRANSACTIONISOLATIONLEVEL: "transaction isolation level">
| <AUTOCOMMIT:                "auto commit">
| <READCOMMITTED:             "read committed">
| <READUNCOMMITTED:           "read uncommitted">
| <REPEATABLEREAD:            "repeatable read">
| <SERIALIZABLE:              "serializable">

| <CASCADE:                   "cascade">

// Current date/time/timestamp literals

| <CURRENT_TIME:      "current_time">
| <CURRENT_DATE:      "current_date">
| <CURRENT_TIMESTAMP: "current_timestamp">



//| <DATA_TYPE: "boolean" | "bit" | "tinyint" | "smallint" | "integer" | "bigint" | "float" | "real" |
//              "double" | "numeric" | "decimal" | "char" | "varchar" | "longvarchar" | "string" |
//              "date" | "time" | "timestamp" | "binary" | "varbinary" | "longvarbinary" >

// NOTE: OPERATOR doesn't match '*' or '=' because we use * and = to mean different things
//  <OPERATOR: ( "==" | ">" | "<" | ">=" | "<=" | "!=" | "<>" | "/" | "+" | "-" |
//               "like" | "not like" | "regex" | "and" | "or" ) >

| <LIKE:       "like" >
//| <NOTLIKE:    "not like" >
| <REGEX:      "regex" >
| <AND:        "and" >
| <OR:         "or" >
| <IN:         "in" >
//| <NOTIN:      "not in" >

| <NOT:        "not">

}

TOKEN : {

    <NUMBER_LITERAL:
       ( ( (["0"-"9"])+ ( "." (["0"-"9"])+ )? )
    | ( "." (["0"-"9"])+ ) )
               ( "E" (["-","+"])? (["0"-"9"])+ )? 
//       (["-","+"])? ( ( (["0"-"9"])+ ( "." (["0"-"9"])+ )? )
//                 |( "." (["0"-"9"])+ ) )
//                            ( "E" (["-","+"])? (["0"-"9"])+ )? 
    
//        ("-")? (["0"-"9"])+ "." (["0"-"9"])*
//      | ("-")? "." (["0"-"9"])+
//      | ("-")? (["0"-"9"])+
//      | 
    >
  | <STRING_LITERAL:   "'" ( "''" | "\\" ["a"-"z", "\\", "%", "_", "'"] | ~["'","\\"] )* "'" >
//  | <STRING_LITERAL:   "'" ( "''" | ~["'","\n","\r","\\"] )* "'" >
//  | <QUOTED_VARIABLE: "\"" ( ["a"-"z", "A"-"Z", "_", "0"-"9", "."] )* "\"" >
  | <QUOTED_VARIABLE:   "\"" ( ~["\""] )* "\"" >
  
}

TOKEN : {  /* IDENTIFIERS */

  <IDENTIFIER: <LETTER> ( <LETTER> | <DIGIT> )* >
| <DOT_DELIMINATED_REF: <IDENTIFIER> ( "." <IDENTIFIER> )* >
| <QUOTED_DELIMINATED_REF: <QUOTED_VARIABLE> ( "." <QUOTED_VARIABLE> )* >
| <JAVA_OBJECT_ARRAY_REF: <DOT_DELIMINATED_REF> "[]" >
| <CTALIAS: <IDENTIFIER> >
| <GLOBVARIABLE: <DOT_DELIMINATED_REF> ".*" >
| <QUOTEDGLOBVARIABLE: <QUOTED_DELIMINATED_REF> ".*" >
| <PARAMETER_REF: "?" >

| <#LETTER: ["a"-"z", "A"-"Z", "_"] >
| <#DIGIT: ["0"-"9"]>

}


void Test() :
{  }
{
  ( parseExpression() ";" )
  
  { }
}


// Parses a single expression.  Useed in 'com.pony.database.Expression.parse' method.
Expression parseExpression() :
{ Expression exp;
}
{
  exp = DoExpression() <EOF>
  
  { return exp; }
}

// Statement that ends with a ';'
StatementTree Statement() :
{ StatementTree ob; }
{
  (
    (   ob=Select()
      | ob=Update()
      | ob=Alter()
      | ob=Compact()
      | ob=Create()
      | ob=Drop()
      | ob=Delete()
      | ob=Insert()
      | ob=Describe()
      | ob=Show()
      | ob=Call()

      | ob=Grant()
      | ob=Revoke()
      
      | ob=CompleteTransaction()    // Either 'commit' or 'rollback'
      | ob=Set()
      
      | ob=ShutDown()
    )
    ( ";" | <EOF> )
  )

  { return ob; }
}
  
// All statements that start with <CREATE>
StatementTree Create() :
{ StatementTree ob;
}
{
  (  <CREATE> 
    (   ob=CreateTable()
      | ob=CreateTrigger()
      | ob=CreateFunction()
      | ob=CreateIndex()
      | ob=CreateSchema()
      | ob=CreateSequence()
      | ob=CreateUser()
      | ob=CreateView()
    )
  )
  
  { return ob; }
}  


// All statements that start with <DROP>
StatementTree Drop() :
{ StatementTree ob;
}
{
  (  <DROP>
    (   ob=DropTable()
      | ob=DropTrigger()
      | ob=DropFunction()
      | ob=DropIndex()
      | ob=DropSchema()
      | ob=DropSequence()
      | ob=DropUser()
      | ob=DropView()
    )
  )
  
  { return ob; }
}




StatementTree Select() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.Select");
  TableSelectExpression table_expr;
  ArrayList order_by = new ArrayList();
  int limit = -1;
}
{
  ( table_expr = GetTableSelectExpression()
    [ <ORDERBY> SelectOrderByList(order_by) ]
	[ <LIMIT> limit = PositiveIntegerConstant() ] 
  )

  { cmd.putObject("table_expression", table_expr);
    cmd.putObject("order_by", order_by);
	cmd.putInt("limit", limit);
    return cmd; }
}


StatementTree Update() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.UpdateTable");
  String table_name;
  ArrayList assignments = new ArrayList();
  SearchExpression where_clause = new SearchExpression();
  int limit = -1;
}
{
  ( <UPDATE> table_name = TableName() <SET> AssignmentList(assignments)
        [ <WHERE> ConditionsExpression(where_clause) ]
        [ <LIMIT> limit = PositiveIntegerConstant() ] 
  )

  { cmd.putObject("table_name", table_name);
    cmd.putObject("assignments", assignments);
    cmd.putObject("where_clause", where_clause);
    cmd.putInt("limit", limit);
    return cmd; }
}

StatementTree Alter() :
{ StatementTree cmd;
}
{
  (   <ALTER>
    (   cmd=AlterTable()
      | cmd=AlterUser()
    )
  )
  
  { return cmd; }
}


StatementTree AlterTable() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.AlterTable");
  String table_name;
  AlterTableAction action;
  StatementTree create_statement;
}
{
  (   <TABLE> table_name=TableName() action=GetAlterTableAction()
      { cmd.putObject("table_name", table_name);
        cmd.putObject("alter_action", action);
      }

    | <CREATE> create_statement = CreateTable()
      { cmd.putObject("create_statement", create_statement); }
        
  )

  { return cmd; }
}


StatementTree Compact() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.Compact");
  String table_name;
}
{
  (    <COMPACT> <TABLE> table_name=TableName()
  )
  
  { cmd.putObject("table_name", table_name);
    return cmd; }
}


StatementTree CreateTable() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.CreateTable");
  boolean temporary = false;
  boolean only_if_not_exists = false;
  String table_name;
  ArrayList column_list = new ArrayList();
  ArrayList constraint_list = new ArrayList();
  Expression check_expression;
}
{
  (  [ <TEMPORARY> { temporary = true; } ]
       <TABLE>
     [ <IF> <NOT> <EXISTS> { only_if_not_exists = true; } ]
       table_name = TableName()
       ColumnDeclarationList(column_list, constraint_list)
     [ <CHECK> check_expression = DoExpression()
            { ConstraintDef check_constraint = new ConstraintDef();
              check_constraint.setCheck(check_expression);
              constraint_list.add(check_constraint); }
     ]

//     [ CreateOptions(statement) ]
//     [ <AS> statement.select = Select() ]
  )
  
  { cmd.putBoolean("temporary", temporary);
    cmd.putBoolean("only_if_not_exists", only_if_not_exists);
    cmd.putObject("table_name", table_name);
    cmd.putObject("column_list", column_list);
    cmd.putObject("constraint_list", constraint_list);
    return cmd; }
  
}


StatementTree CreateTrigger() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.CreateTrigger");
  boolean callback;
  String trigger_name;
  ArrayList trigger_types = new ArrayList();
  String table_name;
  String before_after;
  String procedure_name;
  Expression[] procedure_args;
}
{
  (
    ( <CALLBACK> <TRIGGER> trigger_name = TriggerName()
        TriggerTypes(trigger_types)
        <ON> table_name = TableName()
    )
    { cmd.putObject("type", "callback_trigger");
    }

  | ( <TRIGGER> trigger_name = TriggerName()
        before_after = BeforeOrAfter()
        TriggerTypes(trigger_types)
        <ON> table_name = TableName()
        <FOR> <EACH> <ROW> <EXECUTE> <PROCEDURE>
        procedure_name = FunctionName() "(" procedure_args = ExpressionList() ")"
    )
    { cmd.putObject("type", "procedure_trigger");
      cmd.putObject("before_after", before_after);
      cmd.putObject("procedure_name", procedure_name);
      cmd.putObject("procedure_args", procedure_args);
    }
    
  )
      
  { cmd.putObject("trigger_name", trigger_name);
    cmd.putObject("trigger_types", trigger_types);
    cmd.putObject("table_name", table_name);
    return cmd; }

}  


StatementTree DropTrigger() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.DropTrigger");
  String trigger_name;
  String type = null;
}
{
    (   <CALLBACK> <TRIGGER> trigger_name = TriggerName() { type = "callback_trigger"; } )
  | (   <TRIGGER> trigger_name = TriggerName() { type = "procedure_trigger"; } )
  
  { cmd.putObject("trigger_name", trigger_name);
    cmd.putObject("type", type);
    return cmd; }
}  


StatementTree CreateFunction() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.Function");
  cmd.putObject("type", "create");
  String function_name;
  ArrayList arg_names = new ArrayList();
  ArrayList arg_types = new ArrayList();
  Token loc_name;
  TType return_type = null;
}
{
  (   <FUNCTION> function_name = FunctionName()
      "(" ProcParameterList(arg_names, arg_types) ")"
      [ <RETURNS> return_type = GetTType() ]
      <LANGUAGE> <JAVA> <NAME> loc_name = <STRING_LITERAL>
  )

  { cmd.putObject("function_name", function_name);
    cmd.putObject("arg_names", arg_names);
    cmd.putObject("arg_types", arg_types);
    // Note that 'location_name' will be a TObject
    cmd.putObject("location_name",
                  Util.toParamObject(loc_name, case_insensitive_identifiers));
    cmd.putObject("return_type", return_type);
    return cmd;
  }
}  


StatementTree DropFunction() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.Function");
  cmd.putObject("type", "drop");
  String function_name;
}
{
  (   <FUNCTION> function_name = FunctionName() )

  { cmd.putObject("function_name", function_name);
    return cmd;
  }
}  


StatementTree CreateSchema() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.Schema");
  cmd.putObject("type", "create");
  String schema_name;
}
{
  (   <SCHEMA> schema_name = SchemaName() )
  
  { cmd.putObject("schema_name", schema_name);
    return cmd; }
}


StatementTree DropSchema() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.Schema");
  cmd.putObject("type", "drop");
  String schema_name;
}
{
  (   <SCHEMA> schema_name = SchemaName() )
  
  { cmd.putObject("schema_name", schema_name);
    return cmd; }
}


StatementTree CreateView() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.ViewManager");
  String view_name;
  TableSelectExpression select_cmd;
  ArrayList col_list = new ArrayList();
}
{
  (   <VIEW> view_name = TableName() [ "(" BasicColumnList(col_list) ")" ]
        <AS> select_cmd = GetTableSelectExpression() )

  { cmd.putObject("type", "create");
    cmd.putObject("view_name", view_name);
    cmd.putObject("column_list", col_list);
    cmd.putObject("select_expression", select_cmd);
    return cmd; }
}

StatementTree DropView() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.ViewManager");
  String view_name;
}
{
  (   <VIEW> view_name = TableName() )
  
  { cmd.putObject("type", "drop");
    cmd.putObject("view_name", view_name);
    return cmd;
  }
}


StatementTree CreateIndex() :
{
  StatementTree cmd = new StatementTree("com.pony.database.interpret.NoOp");
}
{
  (   [<UNIQUE>] <INDEX> IndexName() <ON>
        TableName() "(" BasicColumnList(new ArrayList()) ")" )
  
  { return cmd; }
}


StatementTree DropTable() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.DropTable");
  boolean only_if_exists = false;
  String table_name;
  ArrayList table_list = new ArrayList();
}
{
  (   <TABLE>
         [ <IF> <EXISTS> { only_if_exists = true; } ]
         table_name = TableName() { table_list.add(table_name); }
           ( "," table_name = TableName() { table_list.add(table_name); } )*
  )
  
  { cmd.putBoolean("only_if_exists", only_if_exists);
    cmd.putObject("table_list", table_list);
    return cmd; }
  
}


StatementTree DropIndex() :
{
  StatementTree cmd = new StatementTree("com.pony.database.interpret.NoOp");
}
{
  (   <INDEX> IndexName() <ON> TableName()
  )
  
  { return cmd; }

}


StatementTree Call() :
{
  StatementTree cmd = new StatementTree("com.pony.database.interpret.Call");
  String proc_name;
  Expression[] args = null;
}
{
  <CALL> proc_name = ProcedureName() "(" args=ExpressionList() ")"
  { cmd.putObject("proc_name", proc_name);
    cmd.putObject("args", args);
    return cmd;
  }
}


StatementTree CreateSequence() :
{
  StatementTree cmd = new StatementTree("com.pony.database.interpret.Sequence");
  cmd.putObject("type", "create");
  String seq_name;
  Expression v;
}
{
  <SEQUENCE> seq_name=SequenceName() { cmd.putObject("seq_name", seq_name); }
  [ <INCREMENT> v = DoExpression() { cmd.putObject("increment", v); } ]
  [ <MINVALUE> v = DoExpression() { cmd.putObject("min_value", v); } ]
  [ <MAXVALUE> v = DoExpression() { cmd.putObject("max_value", v); } ]
  [ <START> v = DoExpression() { cmd.putObject("start", v); } ]
  [ <CACHE> v = DoExpression() { cmd.putObject("cache", v); } ]
  [ <CYCLE> { cmd.putObject("cycle", "yes"); } ]
  
  { return cmd; }
}
 
StatementTree DropSequence() :
{
  StatementTree cmd = new StatementTree("com.pony.database.interpret.Sequence");
  cmd.putObject("type", "drop");
  String seq_name;
}
{
  <SEQUENCE> seq_name=SequenceName() { cmd.putObject("seq_name", seq_name); }
  
  { return cmd; }
}


StatementTree CreateUser() :
{
  StatementTree cmd = new StatementTree("com.pony.database.interpret.UserManager");
  cmd.putObject("type", "CREATE USER");
}
{
  <USER> UserManagerCommand(cmd)
  
  { return cmd; }
}

StatementTree AlterUser() :
{
  StatementTree cmd = new StatementTree("com.pony.database.interpret.UserManager");
  cmd.putObject("type", "ALTER USER");
}
{
  <USER> UserManagerCommand(cmd)
  
  { return cmd; }
}

StatementTree DropUser() :
{
  StatementTree cmd = new StatementTree("com.pony.database.interpret.UserManager");
  cmd.putObject("type", "DROP USER");
  String username;
}
{ 
  <USER> username = UserName()
  
  { cmd.putObject("username", username);
    return cmd; }
}

void UserManagerCommand(StatementTree cmd) :
{
  String username;
  Expression password_exp;
  Expression[] groups_list = null;
  String lock_status = null;
}
{
  (   username = UserName()
      <SET> <PASSWORD> password_exp=DoExpression()
      [ LOOKAHEAD(2) <SET> <GROUPS> groups_list=ExpressionList() ]
      [ <SET> <ACCOUNT> ( <LOCK> { lock_status="LOCK"; } | <UNLOCK> { lock_status="UNLOCK"; } ) ]
  )
  
  { cmd.putObject("username", username);
    cmd.putObject("password_expression", password_exp);
    cmd.putObject("groups_list", groups_list);
    cmd.putObject("lock_status", lock_status);
  }
}




StatementTree Delete() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.Delete");
  String table_name;
  SearchExpression where_clause = new SearchExpression();
  int limit = -1;
}
{

  ( <DELETE> <FROM> table_name = TableName()
        [ <WHERE> ConditionsExpression(where_clause) ]
        [ <LIMIT> limit = PositiveIntegerConstant() ] 
  )

  { cmd.putObject("table_name", table_name);
    cmd.putObject("where_clause", where_clause);
    cmd.putInt("limit", limit);
    return cmd; }

}


StatementTree Insert() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.Insert");
  String table_name;
  ArrayList col_list = new ArrayList();
  ArrayList data_list = new ArrayList(); // ( Array of Expression[] )
  StatementTree select = null;
  ArrayList assignments = new ArrayList();
  String type;
}
{

  ( <INSERT> [ <INTO> ] table_name = TableName()
    (   [ "(" BasicColumnList(col_list) ")" ]
           (   <VALUES> InsertDataList(data_list)  { type = "from_values"; }
             | select = Select()                   { type = "from_select"; }
           )
      | <SET> AssignmentList(assignments)          { type = "from_set"; }
    )
  )

  { cmd.putObject("table_name", table_name);
    cmd.putObject("col_list", col_list);
    cmd.putObject("data_list", data_list);
    cmd.putObject("select", select);
    cmd.putObject("assignments", assignments);
    cmd.putObject("type", type);
    return cmd; }

}


StatementTree Describe() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.Show");
  cmd.putObject("show", "describe_table");
  String table_name;
}
{

  ( <DESCRIBE> table_name = TableName()
  )
  
  { cmd.putObject("table_name", table_name);
    cmd.putObject("where_clause", new SearchExpression());
    return cmd; }

}


StatementTree Show() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.Show");
  Expression[] args = null;
  SearchExpression where_clause = new SearchExpression();
  Token t;
}
{
  ( <SHOW> 
      (   t=<IDENTIFIER>
        | t=<SCHEMA>
      )
      [ "(" args=ExpressionList() ")" ]
      [ <WHERE> ConditionsExpression(where_clause) ] )
  
  { cmd.putObject("show", t.image);
    cmd.putObject("args", args);
    cmd.putObject("where_clause", where_clause);
    return cmd; }

}


StatementTree Grant() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.PrivManager");
  ArrayList priv_list = new ArrayList();
  String priv_object;
  ArrayList grant_to;
  boolean grant_option = false;
}
{
  ( <GRANT> PrivList(priv_list) <ON> priv_object=PrivObject()
    <TO> grant_to=UserNameList(new ArrayList())
    [ <WITH> <GRANT> <OPTION> { grant_option = true; } ]
  )
  
  { cmd.putObject("command", "GRANT");
    cmd.putObject("priv_list", priv_list);
    cmd.putObject("priv_object", priv_object);
    cmd.putObject("grant_to", grant_to);
    cmd.putBoolean("grant_option", grant_option);
    return cmd;
  }
}


StatementTree Revoke() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.PrivManager");
  ArrayList priv_list = new ArrayList();
  String priv_object;
  ArrayList revoke_from;
  boolean revoke_grant_option = false;
}
{
  ( <REVOKE> [ <GRANT> <OPTION> <FOR> { revoke_grant_option = true; } ]
    PrivList(priv_list) <ON> priv_object=PrivObject()
    <FROM> revoke_from=UserNameList(new ArrayList())
  )
  
  { cmd.putObject("command", "REVOKE");
    cmd.putObject("priv_list", priv_list);
    cmd.putObject("priv_object", priv_object);
    cmd.putObject("revoke_from", revoke_from);
    cmd.putBoolean("revoke_grant_option", revoke_grant_option);
    return cmd;
  }
}



StatementTree CompleteTransaction() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.CompleteTransaction");
  String command;
}
{

  (   <COMMIT>   { command = "commit"; }
    | <ROLLBACK> { command = "rollback"; }
  )

  { cmd.putObject("command", command);
    return cmd; }
}

StatementTree Set() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.Set");
  Token t1;
  String value;
  Expression exp;
  String name;
}
{
  <SET>
  (   t1=<IDENTIFIER> <ASSIGNMENT> exp=DoExpression()
        { cmd.putObject("type", "VARSET");
          cmd.putObject("var_name", t1.image);
          cmd.putObject("exp", exp); }
    | <TRANSACTIONISOLATIONLEVEL> (  t1=<SERIALIZABLE> )
        { cmd.putObject("type", "ISOLATIONSET");
          cmd.putObject("var_name", "TRANSACTION ISOLATION LEVEL");
          cmd.putObject("value", t1.image); }
    | <AUTOCOMMIT> ( t1=<ON> | t1=<IDENTIFIER> )
        { cmd.putObject("type", "AUTOCOMMIT");
          cmd.putObject("value", t1.image); }
    | <SCHEMA> name=SchemaName()
        { cmd.putObject("type", "SCHEMA");
          cmd.putObject("value", name); }
  )
  
  { return cmd; }
}



StatementTree ShutDown() :
{ StatementTree cmd = new StatementTree("com.pony.database.interpret.Misc");
}
{

  <SHUTDOWN>
  
  { cmd.putObject("command", "shutdown");
    return cmd; }
}





// ----------

String TriggerType() :
{ }
{
  (   <INSERT> { return "insert"; }
    | <DELETE> { return "delete"; }
    | <UPDATE> { return "update"; }
  )
}

String BeforeOrAfter() :
{ }
{
  (   <BEFORE> { return "before"; }
    | <AFTER> { return "after"; }
  )
}

// A list of triggered actions separated by 'OR' delimination, for example,
// INSERT OR DELETE OR UPDATE
void TriggerTypes(ArrayList list) :
{ String trig_type;
}
{
  trig_type = TriggerType() { list.add(trig_type); }
    ( <OR> trig_type = TriggerType() { list.add(trig_type); } )*
}

// A priv object
// Note we add a 2 character prefix to the priv object for future enhancements.
// In the future an object may be something other than a table.
String PrivObject() :
{ String table_name;
  String schema_name;
}
{
  (   [ <TABLE> ] table_name=TableName() { return "T:" + table_name; }
    | <SCHEMA> schema_name=SchemaName() { return "S:" + schema_name; }
  )
}

// A list of privs
ArrayList PrivList(ArrayList list) :
{
}
{
        PrivListItem(list)
  ( "," PrivListItem(list) )*
  
  { return list; }
}

// Adds an item in a priv list
void PrivListItem(ArrayList list) :
{ Token t;
}
{
  (   t=<SELECT> | t=<INSERT> | t=<UPDATE> | t=<DELETE>
    | t=<REFERENCES> | t=<USAGE> | t=<ALL> [ <PRIVILEGES> ]
  )
  
  { list.add(t.image); }
}


// A table expression 
TableSelectExpression GetTableSelectExpression() :
{ TableSelectExpression table_expr = new TableSelectExpression();
  String composite = "";
  boolean is_all = false;
  Integer limit = -1;
  TableSelectExpression next_composite_expression;
}
{
  ( <SELECT>
        [ table_expr.distinct = SetQuantifier() ]
        SelectColumnList(table_expr.columns)
        [ <FROM> SelectTableList(table_expr.from_clause) ]
        [ <WHERE> ConditionsExpression(table_expr.where_clause) ]		
		
        [ <GROUPBY> SelectGroupByList(table_expr.group_by)
          [ <GROUPMAX> table_expr.group_max = GroupMaxColumn() ] 
          [ <HAVING> ConditionsExpression(table_expr.having_clause) ] ]

        [ composite = GetComposite() [ <ALL> { is_all = true; } ]
          next_composite_expression = GetTableSelectExpression()
          { table_expr.chainComposite(next_composite_expression, composite, is_all); } 
        ]
  )
  { return table_expr; }
}

AlterTableAction GetAlterTableAction() :
{ String col_name, con_name;
  ColumnDef column_def;
  ConstraintDef constraint_def;
  Expression default_exp;
  AlterTableAction action = new AlterTableAction();
}
{
  (   <SQLADD>
      (   [ <SQLCOLUMN> ] column_def=ColumnDefinition()
          { action.setAction("ADD");
            action.addElement(column_def);
          }
        | constraint_def=TableConstraintDefinition()
          { action.setAction("ADD_CONSTRAINT");
            action.addElement(constraint_def);
          }
      )
    | <ALTER> [ <SQLCOLUMN> ] col_name=ColumnName()
      (   <SET> default_exp=DoExpression()
          { action.setAction("ALTERSET");
            action.addElement(col_name);
            action.addElement(default_exp);
          }
        | <DROP> <SQLDEFAULT>
          { action.setAction("DROPDEFAULT");
            action.addElement(col_name);
          }
      )
    | <DROP>
      (   [ <SQLCOLUMN> ] col_name=ColumnName()
          { action.setAction("DROP");
            action.addElement(col_name);
          }
        | <CONSTRAINT> con_name=ConstraintName()
          { action.setAction("DROP_CONSTRAINT");
            action.addElement(con_name);
          }
        | <PRIMARY> <KEY>
          { action.setAction("DROP_CONSTRAINT_PRIMARY_KEY");
          }
      )
  )
  
  { return action; }
}



// An element to insert, either an expression or DEFAULT for the default
// element.
Object InsertElement() :
{ Expression e;
}
{
  (   <SQLDEFAULT>        { return "DEFAULT"; }
    | e = DoExpression()  { return e; }
  )
}

ArrayList InsertExpressionList() :
{ ArrayList list = new ArrayList();
  Object elem;
}
{
  [ elem = InsertElement() { list.add(elem); }
    ( "," elem = InsertElement() { list.add(elem); }  )*
  ]

  { return list; }
}  

// The list of columns to insert formatted as; eg.  (9, 4), (3, 2), (9, 9), ....
void InsertDataList(ArrayList data_list) :
{ ArrayList insert_vals;
}
{

    "(" insert_vals = InsertExpressionList() ")" { data_list.add(insert_vals); }
  ( "," "(" insert_vals = InsertExpressionList() ")" { data_list.add(insert_vals); } )*

}

// Returning true means distinct, false means all.
boolean SetQuantifier() :
{}
{  ( <DISTINCT> { return true; } |
     <ALL>      { return false; } )
}
     

void SelectColumnList(ArrayList list) :
{ SelectColumn col;
}
{
    col = SelectColumn() { list.add(col); }
  ( "," col = SelectColumn() { list.add(col); } )* 

}

SelectColumn SelectColumn() :
{ SelectColumn col = new SelectColumn();
  String aliased_name;
  Token t;
}
{ 
  (   col.expression = DoExpression() [ <AS> ] [ col.alias=TableAliasName() ]
    | <STAR> { col.glob_name = "*"; }
    | t = <GLOBVARIABLE> { col.glob_name = caseCheck(t.image); }
    | t = <QUOTEDGLOBVARIABLE> { col.glob_name = caseCheck(Util.asNonQuotedRef(t)); }
  )
  { return col; }
}

void SelectGroupByList(ArrayList list) :
{ ByColumn col;
  Expression exp;
}
{
    exp = DoExpression() { col = new ByColumn();
                           col.exp = exp;
                           list.add(col); }
  ( "," exp = DoExpression() { col = new ByColumn();
                               col.exp = exp;
                               list.add(col); } )*
  
}

/**
 * NOTE: This is an extension, allows for us to specify a column to return the
 *  max value for each row representing a group.
 */
Variable GroupMaxColumn() :
{ Variable var; }
{
  var = ColumnNameVariable()
  { return var; }
}



void SelectOrderByList(ArrayList list) :
{ ByColumn col;
  Expression exp;
  boolean ascending = true;
}
{
    exp = DoExpression() [ ascending=OrderingSpec() ]
                         { col = new ByColumn();
                           col.exp = exp;
                           col.ascending = ascending;
                           list.add(col); }
  ( "," exp = DoExpression() { ascending=true; } [ ascending=OrderingSpec() ]
                         { col = new ByColumn();
                           col.exp = exp;
                           col.ascending = ascending;
                           list.add(col); } )*
  
}

boolean OrderingSpec() :
{}
{
  ( <ASC> { return true; } | <DESC> { return false; } )
  
  { return true; }
}


void TableDeclaration(FromClause from_clause) :
{ String table=null, declare_as = null;
  TableSelectExpression select_stmt = null;
}
{

  ( ( table=TableName() | "(" select_stmt=GetTableSelectExpression() ")" )
    [ [ <AS> ] declare_as=TableName() ] )
  
  { from_clause.addTableDeclaration(table, select_stmt, declare_as); }

}

void SelectTableList(FromClause from_clause) :
{}
{
  TableDeclaration(from_clause) [ FromClauseJoin(from_clause) ]
}

void FromClauseJoin(FromClause from_clause) :
{ Expression on_expression; }
{ 
  (
      (
        ","
        { from_clause.addJoin(JoiningSet.INNER_JOIN);}
      ) [ SelectTableList(from_clause) ]
    | (
        [ <INNER> ] <JOIN> TableDeclaration(from_clause) <ON> on_expression=DoExpression()
        { from_clause.addPreviousJoin(JoiningSet.INNER_JOIN, on_expression); }
      ) [ FromClauseJoin(from_clause) ]
    | (
        <LEFT> [<OUTER>] <JOIN> TableDeclaration(from_clause) <ON> on_expression=DoExpression()
        { from_clause.addPreviousJoin(JoiningSet.LEFT_OUTER_JOIN, on_expression); }
      ) [ FromClauseJoin(from_clause) ]
    | (
        <RIGHT> [<OUTER>] <JOIN> TableDeclaration(from_clause) <ON> on_expression=DoExpression()
        { from_clause.addPreviousJoin(JoiningSet.RIGHT_OUTER_JOIN, on_expression); }
      ) [ FromClauseJoin(from_clause) ]
  )

}






// A list of parameters in a function or procedure declaration.  For example,
// ' p1 NUMERIC, p2 NUMERIC, s1 CHARACTER VARYING '
// First array contains parameter names, and second contains TType representing
// the type.
void ProcParameterList(ArrayList decl_names, ArrayList decl_types) :
{ String name;
  TType type;
}
{
  [ { name = null; }
    ( [ name = ProcArgumentName() ] type = GetTType() ) { decl_names.add(name);
                                                          decl_types.add(type); }
    ( ( "," { name = null; }
        [ name = ProcArgumentName() ] type = GetTType() ) { decl_names.add(name);
                                                            decl_types.add(type); }
    )*
  ]
}


// The ' set a = (a * 9), b = concat(b, "aa") ' part of the 'update', 'insert' statement
void AssignmentList(ArrayList assignment_list) :
{ String column;
  Expression exp;
}
{
  ( column=ColumnName() <ASSIGNMENT> exp=DoExpression()
    { assignment_list.add(new Assignment(Variable.resolve(column), exp)); }
    [ "," AssignmentList(assignment_list) ]   
  )
}

// Parses a list of column declarations.  eg. ' id NUMERIC(5, 20), number VARCHAR(90), ... '
// and also any constraints.
void ColumnDeclarationList(ArrayList column_list, ArrayList constraint_list) :
{
}
{
  "(" ColumnOrConstraintDefinition(column_list, constraint_list)
      ( "," ColumnOrConstraintDefinition(column_list, constraint_list) )*
  ")"
}

void ColumnOrConstraintDefinition(ArrayList column_list, ArrayList constraint_list) :
{ ColumnDef coldef = null;
  ConstraintDef condef = null;
}
{
  (   coldef = ColumnDefinition()           { column_list.add(coldef); }
    | condef = TableConstraintDefinition()  { constraint_list.add(condef); }
  )
}

ColumnDef ColumnDefinition() :
{ ColumnDef column = new ColumnDef();
  Token t;
  Token col_constraint;
  Expression default_exp;
  String col_name;
}
{
  ( col_name = ColumnName() { column.setName(col_name); }
    ColumnDataType(column)

    [ <SQLDEFAULT> default_exp = DoExpression() { column.setDefaultExpression(default_exp); } ]
    ( ColumnConstraint(column) )*
    [ ( t=<INDEX_BLIST> | t=<INDEX_NONE> ) { column.setIndex(t); } ] 
  )
  
  { return column; }

}

// Constraint on a column, eg. 'NOT NULL', 'NULL', 'PRIMARY KEY', 'UNIQUE', etc.
void ColumnConstraint(ColumnDef column) :
{ Token t;
  String table_name;
  ArrayList col_list = new ArrayList();
}
{
    (   <NOT> <NULL_LITERAL>  { column.addConstraint("NOT NULL"); }
      | <NULL_LITERAL> { column.addConstraint("NULL"); }
      | <PRIMARY> <KEY> { column.addConstraint("PRIMARY"); }
      | <UNIQUE> { column.addConstraint("UNIQUE"); }
    )
    
}


int GetCollateStrength() :
{ 
}
{ (   <PRIMARY_STRENGTH>    { return java.text.Collator.PRIMARY; }
    | <SECONDARY_STRENGTH>  { return java.text.Collator.SECONDARY; }
    | <TERTIARY_STRENGTH>   { return java.text.Collator.TERTIARY; }
    | <IDENTICAL_STRENGTH>  { return java.text.Collator.IDENTICAL; }
  )
}

int GetCollateDecomposition() :
{ }
{ (   <NO_DECOMPOSITION>        { return java.text.Collator.NO_DECOMPOSITION; }
    | <CANONICAL_DECOMPOSITION> { return java.text.Collator.CANONICAL_DECOMPOSITION; }
    | <FULL_DECOMPOSITION>      { return java.text.Collator.FULL_DECOMPOSITION; }
  )
}


int GetStringSQLType() :
{ }
{
    LOOKAHEAD(2) ( <CHARACTER> <VARYING> ) { return SQLTypes.VARCHAR; }
  | LOOKAHEAD(3) ( <LONG> <CHARACTER> <VARYING> ) { return SQLTypes.LONGVARCHAR; }
  | ( <TEXT> | <STRING> | <LONGVARCHAR> ) { return SQLTypes.LONGVARCHAR; }
  | ( <CHAR> | <CHARACTER> ) { return SQLTypes.CHAR; }
  | <VARCHAR> { return SQLTypes.VARCHAR; }
  | <CLOB> { return SQLTypes.CLOB; }
}

int GetNumericSQLType() :
{ }
{
    ( <INT> | <INTEGER> ) { return SQLTypes.INTEGER; }
  | <TINYINT> { return SQLTypes.TINYINT; }
  | <SMALLINT> { return SQLTypes.SMALLINT; }
  | <BIGINT> { return SQLTypes.BIGINT; }
  | <FLOAT> { return SQLTypes.FLOAT; }
  | <REAL> { return SQLTypes.REAL; }
  | <DOUBLE> { return SQLTypes.DOUBLE; }
  | <NUMERIC> { return SQLTypes.NUMERIC; }
  | <DECIMAL> { return SQLTypes.DECIMAL; }
}

int GetBooleanSQLType() :
{ }
{
  ( <BOOLEAN> | <BIT> ) { return SQLTypes.BOOLEAN; }
}

int GetDateSQLType() :
{ }
{
    <TIMESTAMP> { return SQLTypes.TIMESTAMP; }
  | <TIME> { return SQLTypes.TIME; }
  | <DATE> { return SQLTypes.DATE; }
}

int GetBinarySQLType() :
{ }
{
    LOOKAHEAD(2) ( <BINARY> <VARYING> ) { return SQLTypes.VARBINARY; }
  | LOOKAHEAD(3) ( <LONG> <BINARY> <VARYING> ) { return SQLTypes.LONGVARBINARY; }
  | <LONGVARBINARY> { return SQLTypes.LONGVARBINARY; }
  | <VARBINARY> { return SQLTypes.VARBINARY; }
  | <BINARY> { return SQLTypes.BINARY; }
  | <BLOB> { return SQLTypes.BLOB; }
}

// Parses an SQL type and forms a TType object.  For example, "CHAR(500)" is
// parsed to a TStringType with a maximum size of 500 and lexicographical
// collation.
TType GetTType() :
{ Token t;
  int data_type;
  int size = -1;
  int scale = -1;
  Token class_tok = null;
  int strength = -1;
  int decomposition = -1;
  String loc = null;
}
{
  (   <JAVA_OBJECT> [ "(" ( class_tok=<DOT_DELIMINATED_REF> | class_tok=<JAVA_OBJECT_ARRAY_REF> ) ")" ]
      { String class_str = "java.lang.Object";
        if (class_tok != null) {
          class_str = class_tok.image;
        }
        return TType.javaObjectType(class_str);
      } 

    | LOOKAHEAD(GetStringSQLType())
      data_type = GetStringSQLType() [ "(" size = PositiveIntegerConstant() ")" ]
      [ <COLLATE> t=<STRING_LITERAL> { loc = ((TObject) Util.toParamObject(t, case_insensitive_identifiers)).toString(); }
         [ strength=GetCollateStrength() ] [ decomposition = GetCollateDecomposition() ] ]
      { return TType.stringType(data_type, size, loc, strength, decomposition); }

    | data_type = GetNumericSQLType() [ "(" size = PositiveIntegerConstant()
                             [ "," scale = PositiveIntegerConstant() ] ")" ]
      { return TType.numericType(data_type, size, scale); } 
      
    | data_type = GetBooleanSQLType()
      { return TType.booleanType(data_type); }

    | data_type = GetDateSQLType()
      { return TType.dateType(data_type); }

    | data_type = GetBinarySQLType() [ "(" size = PositiveIntegerConstant() ")" ]
      { return TType.binaryType(data_type, size); }

  )
}

// Data type of a ColumnDef (eg. "varchar(50)", etc)
void ColumnDataType(ColumnDef column) :
{ TType type;
}
{
  type = GetTType() { column.setDataType(type); }
}


ConstraintDef TableConstraintDefinition() :
{ ConstraintDef constraint = new ConstraintDef();
  ArrayList column_list = new ArrayList();
  ArrayList column_list2 = new ArrayList();
  String constraint_name;
  String update_rule = "NO ACTION";
  String delete_rule = "NO ACTION";
  Expression expression;
  String name;
  String reference_table;
  Token t;
}
{
  ( [ <CONSTRAINT> constraint_name = ConstraintName() { constraint.setName(constraint_name); }  ]

    (   <PRIMARY> <KEY> "(" BasicColumnList(column_list) ")" { constraint.setPrimaryKey(column_list); }
      | <UNIQUE> "(" BasicColumnList(column_list) ")"      { constraint.setUnique(column_list); }
      | <CHECK> "(" expression = DoExpression() ")"        { constraint.setCheck(expression); }
      | <FOREIGN> <KEY> "(" BasicColumnList(column_list) ")"
        <REFERENCES> reference_table=TableName() [ "(" BasicColumnList(column_list2) ")" ]
        [   LOOKAHEAD(2) ( <ON> <DELETE> delete_rule=ReferentialTrigger()
              [ <ON> <UPDATE> update_rule=ReferentialTrigger() ]
            )
          | ( <ON> <UPDATE> update_rule=ReferentialTrigger()
              [ <ON> <DELETE> delete_rule=ReferentialTrigger() ]
            )
        ]
        { constraint.setForeignKey(reference_table, column_list, column_list2, delete_rule, update_rule); }
    )

    // Constraint deferrability
    [ ConstraintAttributes(constraint) ]
   
  )
  
  { return constraint; }

}

String ReferentialTrigger() :
{ Token t;
  String trigger_str;
}
{
  (   <NO> <ACTION>                     { trigger_str="NO ACTION"; }
    | <RESTRICT>                        { trigger_str="NO ACTION"; }
    | <CASCADE>                         { trigger_str="CASCADE"; }
    | LOOKAHEAD(2) <SET> <NULL_LITERAL> { trigger_str="SET NULL"; }
    | <SET> <SQLDEFAULT>                { trigger_str="SET DEFAULT"; }
  )

  { return trigger_str; }
}

void ConstraintAttributes(ConstraintDef constraint) :
{
}
{
  (   (
        <INITIALLY> (   <DEFERRED>  { constraint.setInitiallyDeferred(); }
                      | <IMMEDIATE>
                    )

        [   <NOT> <DEFERRABLE>  { constraint.setNotDeferrable(); }
          | <DEFERRABLE>
        ]
      )
    |
      (
        ( <NOT> <DEFERRABLE>  { constraint.setNotDeferrable(); } | <DEFERRABLE> )
        [ <INITIALLY> (   <DEFERRED>  { constraint.setInitiallyDeferred(); }
                        | <IMMEDIATE>
                      )
        ]
      )
  )
}



// A list of column names
ArrayList BasicColumnList(ArrayList list) :
{ String col_name;
}
{
        col_name = ColumnName() { list.add(col_name); }
  ( "," col_name = ColumnName() { list.add(col_name); } )*

  { return list; }
}

// A list of user names
ArrayList UserNameList(ArrayList list) :
{ String username;
}
{
        username = UserName() { list.add(username); }
  ( "," username = UserName() { list.add(username); } )*

  { return list; }
}



void ConditionsExpression(SearchExpression se) :
{ Expression exp; }
{
  exp = DoExpression()

  { se.setFromExpression(exp); }
}



Expression ExpressionTest() :
{ Expression exp; }
{
  exp=DoExpression() ";"

  { return exp; }
}  

Expression DoExpression() :
{ Stack stack = new Stack();
  Expression exp = new Expression();
}
{

  expression(exp, stack)

  { expEnd(exp, stack);
    // Normalize the expression (remove any NOT operators)
    Expression normalized_exp = Util.normalize(exp);
    normalized_exp.copyTextFrom(exp);
    return normalized_exp;
  }
}

Expression DoNonBooleanExpression() :
{ Stack stack = new Stack();
  Expression exp = new Expression();
}
{

  nonBooleanExpression(exp, stack)

  { expEnd(exp, stack);
    return exp; }
}


/**
 * Parse an expression.
 */
void expression(Expression exp, Stack stack) :
{ 
}
{

  Operand(exp, stack) ( LOOKAHEAD(2) OpPart(exp, stack) )*

}

/**
 * Parses a non-boolean expression.
 */
void nonBooleanExpression(Expression exp, Stack stack) :
{
}
{
  
  Operand(exp, stack)
     ( LOOKAHEAD(2) (  StringOperator(exp, stack)
                     | NumericOperator(exp, stack) ) Operand(exp, stack) )* 

}

void OpPart(Expression exp, Stack stack) :
{ Token t;
//  SelectStatement select;
//  Expression[] exp_arr;
  Expression regex_expression;
  Object regex_ob;
  
}
{

  (   LOOKAHEAD(3) (   BooleanOperator(exp, stack)
                     | NumericOperator(exp, stack)
                     | StringOperator(exp, stack) )
                   Operand(exp, stack)

      // NOTE: Handling regex literals is a horrible horrible hack.  The <REGEX_LITERAL> 
      //   token starts with 'regex /' and the regex string follows.
    | (   <REGEX> { exp.text().append(" regex ");
                                 expOperator(exp, stack, Operator.get("regex"));
                               }
                               expression(exp, stack)
                     | t=<REGEX_LITERAL>
                        { regex_ob = Util.toParamObject(t, case_insensitive_identifiers);
                          exp.text().append(" regex " + regex_ob);
                          expOperator(exp, stack, Operator.get("regex"));
                          exp.addElement(regex_ob);
                        }
                   )

    | LOOKAHEAD(2) SubQueryOperator(exp, stack) SubQueryExpression(exp, stack) 

    | BetweenPredicate(exp, stack) 

  )
  
}  
  

void Operand(Expression exp, Stack stack) :
{ Token t, tt;
  FunctionDef f;
  Expression[] exp_list;
  String time_fname;
  boolean negative = false;
  Object param_ob;
}
{
  (   "(" { stack.push(Operator.get("(")); exp.text().append("("); }
        expression(exp, stack) ")" { expEndParen(exp, stack); exp.text().append(")"); }
    | t = <PARAMETER_REF>
          { Object param_resolve = createSubstitution(t.image); 
            exp.addElement(param_resolve);
            exp.text().append('?');
          }

    | LOOKAHEAD(2) <NOT>
      { expOperator(exp, stack, Operator.get("not"));
        exp.text().append(" not ");
      }
      Operand(exp, stack)
          
    | LOOKAHEAD(3) ( f = Function()
          { exp.addElement(f); exp.text().append(f); }
      ) 

// Time values
| ( (   tt=<DATE> { time_fname="DATEOB"; }
      | tt=<TIME> { time_fname="TIMEOB"; } 
      | tt=<TIMESTAMP> { time_fname="TIMESTAMPOB"; }
    )
    t=<STRING_LITERAL>
    { Object param_ob1 = Util.toParamObject(t, case_insensitive_identifiers);
      exp_list = new Expression[] { new Expression(param_ob1) };
      f = Util.resolveFunctionName(time_fname, exp_list);
      exp.addElement(f);
      exp.text().append(tt.image).append(" ").append(t.image);
    }
  )

// Current timestamp
| ( (   tt=<CURRENT_TIMESTAMP> { time_fname="TIMESTAMPOB"; }
      | tt=<CURRENT_TIME>      { time_fname="TIMEOB"; }
      | tt=<CURRENT_DATE>      { time_fname="DATEOB"; }
    )
    { exp_list = new Expression[0];
      f = Util.resolveFunctionName(time_fname, exp_list);
      exp.addElement(f);
      exp.text().append(tt.image);
    }
  )
      
// Java object instantiation
    | ( <NEW> f = JavaInstantiation()
          { exp.addElement(f); exp.text().append(f); }
      )
          
    | (   t=<STRING_LITERAL>
        | t=<BOOLEAN_LITERAL>
        | t=<NULL_LITERAL>
      ) { param_ob = Util.toParamObject(t, case_insensitive_identifiers); 
          exp.addElement(param_ob);
          exp.text().append(t.image);
        }
        
    | ( [ <ADD> | <SUBTRACT> { negative = true; } ]
        (
            t=<NUMBER_LITERAL>
          | t=<QUOTED_VARIABLE>        // (eg. '"rel1"')
          | t=<DOT_DELIMINATED_REF>    // (eg. 're1.re2.id')
          | t=<QUOTED_DELIMINATED_REF> // (eg. '"re1"."re2"."id"')
          | t=SQLIdentifier()
        )
      ) { if (t.kind == SQLConstants.NUMBER_LITERAL) {
            param_ob = Util.parseNumberToken(t, negative);
            exp.addElement(param_ob);
          }
          else {
            param_ob = Util.toParamObject(t, case_insensitive_identifiers); 
            if (negative) {
              exp.addElement(Util.zeroNumber());
              exp.addElement(param_ob);
              exp.addElement(Operator.get("-"));
            }
            else {
              exp.addElement(param_ob);
            }
          }
          if (negative) {
            exp.text().append('-');
          }
          exp.text().append(t.image);
          
        }

  )

}

void SubQueryExpression(Expression exp, Stack stack) :
{ TableSelectExpression select;
  Expression[] exp_arr;
}
{
  // Parse the subquery list (either a list or a select statement)
  "("
  (   select=GetTableSelectExpression()
      { exp.addElement(select);
        exp.text().append(" [SELECT]"); }
    | exp_arr=ExpressionList()
      { exp.addElement(Util.toArrayParamObject(exp_arr));
        exp.text().append(" (" + Util.expressionListToString(exp_arr) + ")");
      }
  )
  ")"
}


// Parses a simple positive integer constant.
int PositiveIntegerConstant() :
{ Token t;
}
{
  t = <NUMBER_LITERAL>

  { int val = Integer.parseInt(t.image);
    if (val < 0) generateParseException();
    return val;
  }
}


void SubQueryOperator(Expression exp, Stack stack) :
{ Token t;
  String op_string;
  String query_type = "SINGLE";
  Operator op;
}
{
  (   LOOKAHEAD(2) (   <IN> { op = Operator.get("IN"); }
                     | <NOT> <IN> { op = Operator.get("NOT IN"); }
      )
  
    | (  op_string = GetSubQueryBooleanOperator() { op = Operator.get(op_string); }
    [ ( t=<ANY> | t=<ALL> | t=<SOME> ) { query_type=t.image; } ]
    { op = op.getSubQueryForm(query_type);
    }
      )
  )

  { expOperator(exp, stack, op);
    exp.text().append(" " + op + " ");
  }          
  
}

void BetweenPredicate(Expression exp, Stack stack) :
{ boolean not_s = false;
  Expression exp1, exp2;
}
{   [ <NOT> { not_s = true; } ] <BETWEEN>
           exp1=DoNonBooleanExpression() <AND> exp2=DoNonBooleanExpression()

  { // Flush the operator stack to precedence 8
    flushOperatorStack(exp, stack, 8);
    // Get the end expression
    Expression end_exp = exp.getEndExpression();
    if (not_s) {
      exp.concat(exp1);
      exp.addElement(Operator.get("<"));
      exp.concat(end_exp);
      exp.concat(exp2);
      exp.addElement(Operator.get(">"));
      exp.addElement(Operator.get("or"));
      exp.text().append(" not between ");
    }
    else {
      exp.concat(exp1);
      exp.addElement(Operator.get(">="));
      exp.concat(end_exp);
      exp.concat(exp2);
      exp.addElement(Operator.get("<="));
      exp.addElement(Operator.get("and"));
      exp.text().append(" between ");
    }
    exp.text().append(exp1.text().toString());
    exp.text().append(" and ");
    exp.text().append(exp2.text().toString());

  }

}

void BooleanOperator(Expression exp, Stack stack) :
{ Token t;
  String op_string;
  Operator op;
}
{
  (   op_string = GetBooleanOperator() { op = Operator.get(op_string); }
  )

  { expOperator(exp, stack, op);
    exp.text().append(" " + op + " ");
  }          
}

void NumericOperator(Expression exp, Stack stack) :
{ Token t;
  String op_string;
  Operator op;
}
{
  (   op_string = GetNumericOperator() { op = Operator.get(op_string); }
  )

  { expOperator(exp, stack, op);
    exp.text().append(" " + op + " ");
  }          
}

void StringOperator(Expression exp, Stack stack) :
{ Token t;
  String op_string;
  Operator op;
}
{
  (   op_string = GetStringOperator() { op = Operator.get(op_string); }
  )

  { expOperator(exp, stack, op);
    exp.text().append(" " + op + " ");
  }          
}

String GetBooleanOperator() :
{ Token t;
}
{
  (   t=<ASSIGNMENT> | t=<EQUALS> | t=<GR> | t=<LE> | t=<GREQ> | t=<LEEQ> | t=<NOTEQ>
    | LOOKAHEAD(2) <IS> <NOT> { return "IS NOT"; } | t=<IS> 
    | t=<LIKE> | <NOT> <LIKE> { return "NOT LIKE"; }
    | t=<AND> | t=<OR>
  )
  { return t.image; }
}

String GetSubQueryBooleanOperator() :
{ Token t;
}
{
  (   t=<ASSIGNMENT> | t=<EQUALS> | t=<GR> | t=<LE> | t=<GREQ> | t=<LEEQ> | t=<NOTEQ> 
  )
  { return t.image; }
}

String GetNumericOperator() :
{ Token t;
}
{
   (   t=<DIVIDE> | t=<ADD> | t=<SUBTRACT>
     | t=<STAR>   // This is "*" (multiply) 
   )
   { return t.image; }
}

String GetStringOperator() :
{ Token t;
}
{
   ( t=<CONCAT> )
   { return t.image; }
}



Token FunctionIdentifier() :
{ Token t;
}
{
  ( t = <IF> | t = <USER> | t = <IDENTIFIER> )
  { return t; }
}

FunctionDef Function() :
{ Token t, t2 = null, t3 = null;
  FunctionDef f;
  Expression exp1, exp2;
  Expression[] exp_list;
  TType cast_type;
}
{
  ( // COUNT function requires special handling,
      ( t = <COUNT> "(" [ <DISTINCT> { t.image="distinct_count"; } ] exp_list = FunctionParams() ")" )
    // TRIM function  
    | ( t = <TRIM> "(" [ LOOKAHEAD(3) [ t2=<LEADING> | t2=<BOTH> | t2=<TRAILING> ]
                         [ t3=<STRING_LITERAL> ] <FROM> ] exp1=DoExpression() ")" ) 
                        { exp_list = new Expression[3];
                          String ttype = t2 == null ? "both" : t2.image.toLowerCase();
                          Object str_char = t3 == null ? TObject.stringVal(" ") :
                                                         Util.toParamObject(t3, case_insensitive_identifiers);
                          exp_list[0] = new Expression(TObject.stringVal(ttype));
                          exp_list[0].text().append("'" + ttype + "'");
                          exp_list[1] = new Expression(str_char);
                          exp_list[1].text().append("'" + str_char + "'");
                          exp_list[2] = exp1;
                          return Util.resolveFunctionName("sql_trim", exp_list);
                        }
    // CAST function
    | ( t = <CAST> "(" exp1=DoExpression() <AS> cast_type=GetTType() ")" )
                        { exp_list = new Expression[2];
                          String enc_form = TType.asEncodedString(cast_type);
                          exp_list[0] = exp1;
                          exp_list[1] = new Expression(TObject.stringVal(enc_form));
                          exp_list[1].text().append("'" + enc_form + "'");
                          return Util.resolveFunctionName("sql_cast", exp_list);
                        }
    // Parse a function identifier and function parameter list.
    | ( t = FunctionIdentifier() "(" exp_list = FunctionParams() ")" )
//    // IF function
//    | ( t = <IF> "(" exp_list = FunctionParams() ")" )
//    // Standard functions.
//    | ( t = <IDENTIFIER> "(" exp_list = FunctionParams() ")" )
  )

  { return Util.resolveFunctionName(t.image, exp_list); }  
}

// An instantiation of a java object.  For example, 'java.awt.Point(40, 30)'
FunctionDef JavaInstantiation() :
{ Token t;
  Expression[] args;
}
{
  // PENDING: Handling arrays (eg. 'java.lang.String[] { 'Tobias', 'Downer' }' or 'double[] { 25, 2, 75, 26 }' )
  t=<DOT_DELIMINATED_REF> "(" args=ExpressionList() ")"
  
  { Expression[] comp_args = new Expression[args.length + 1];
    System.arraycopy(args, 0, comp_args, 1, args.length);
    comp_args[0] = new Expression(TObject.stringVal(t.image));
    comp_args[0].text().append("'" + t.image + "'");
    return Util.resolveFunctionName("_new_JavaObject", comp_args); }
}

// Parameters for a function
Expression[] FunctionParams() :
{ Expression[] exp_list;
}
{
  ( <STAR> { exp_list = FunctionFactory.GLOB_LIST; }
    | exp_list = ExpressionList()
  )
  
  { return exp_list; }
}



Expression[] ExpressionList() :
{ ArrayList list = new ArrayList();
  Expression e;
}
{
  [ e = DoExpression() { list.add(e); }
    ( "," e = DoExpression() { list.add(e); }  )*
  ]
  
  { return (Expression[]) list.toArray(new Expression[list.size()]); }
}


String GetComposite() :
{ Token name; }
{
  ( name = <UNION> | name = <INTERSECT> | name = <EXCEPT> )
  
  { return name.image; }
}



String TableName() :
{ Token name;
}
{
  ( name = <QUOTED_VARIABLE> | name = SQLIdentifier() |
    name = <OLD> | name = <NEW> |
    name = <DOT_DELIMINATED_REF> | name = <QUOTED_DELIMINATED_REF> )

  { return caseCheck(Util.asNonQuotedRef(name)); }

}

String SequenceName() :
{ Token name;
}
{
  ( name = <QUOTED_VARIABLE> | name = SQLIdentifier() |
    name = <DOT_DELIMINATED_REF> | name = <QUOTED_DELIMINATED_REF> )

  { return caseCheck(Util.asNonQuotedRef(name)); }
}

String TriggerName() :
{ Token name;
}
{
  ( name = <QUOTED_VARIABLE> | name = SQLIdentifier() )

  { return caseCheck(Util.asNonQuotedRef(name)); }
}

String IndexName() :
{ Token name;
}
{
  ( name = <QUOTED_VARIABLE> | name = SQLIdentifier() )

  { return caseCheck(Util.asNonQuotedRef(name)); }
}

// A username
String UserName() :
{ Token name; }
{
  ( name = <QUOTED_VARIABLE> | name = <IDENTIFIER> | name = <PUBLIC> )
  
  { return caseCheck(Util.asNonQuotedRef(name)); }
}

// Name of a schema
String SchemaName() :
{ Token name; }
{
  ( name = <QUOTED_VARIABLE> | name = SQLIdentifier() )
  
  { return caseCheck(Util.asNonQuotedRef(name)); }
}

// Name of a constraint name
String ConstraintName() :
{ Token name; }
{
  ( name = <QUOTED_VARIABLE> | name = SQLIdentifier() )
  
  { return caseCheck(Util.asNonQuotedRef(name)); }
}

// Parses a column name  
String ColumnName() :
{ Token name; }
{
  ( name = <QUOTED_VARIABLE> | name = SQLIdentifier() |
    name = <DOT_DELIMINATED_REF> | name = <QUOTED_DELIMINATED_REF> )

  { return caseCheck(Util.asNonQuotedRef(name)); }
}

// Parses a column name as a Variable object  
Variable ColumnNameVariable() :
{ Token name; }
{
  ( name = <QUOTED_VARIABLE> | name = SQLIdentifier() |
    name = <DOT_DELIMINATED_REF> | name = <QUOTED_DELIMINATED_REF> )

  { return (Variable) Util.toParamObject(name, case_insensitive_identifiers); } 
}

// Parses an aliased table name  
String TableAliasName() :
{ Token name; }
{
  ( name = <QUOTED_VARIABLE> | name = SQLIdentifier() )

  { return caseCheck(Util.asNonQuotedRef(name)); }
}

// Parses a procedure name  
String ProcedureName() :
{ Token name; }
{
  ( name = <QUOTED_VARIABLE> | name = SQLIdentifier() |
    name = <DOT_DELIMINATED_REF> | name = <QUOTED_DELIMINATED_REF> )

  { return caseCheck(Util.asNonQuotedRef(name)); }
}

// Parses a function name
String FunctionName() :
{ Token name; }
{
  ( name = <QUOTED_VARIABLE> | name = SQLIdentifier() |
    name = <DOT_DELIMINATED_REF> | name = <QUOTED_DELIMINATED_REF> )

  { return caseCheck(Util.asNonQuotedRef(name)); }
}

// Parses the name of an argument in a procedure/function declaration
String ProcArgumentName() :
{ Token name; }
{
  ( name = <QUOTED_VARIABLE> | name = SQLIdentifier() )
  
  { return caseCheck(Util.asNonQuotedRef(name)); }
}

// Parses an SQL identifier
Token SQLIdentifier() :
{ Token name; }
{
  (   name = <IDENTIFIER>
    | name = <OPTION> | name = <ACCOUNT> | name = <PASSWORD>
    | name = <PRIVILEGES> | name = <GROUPS> | name = <LANGUAGE>
    | name = <NAME> | name = <JAVA> | name = <ACTION>
  )
  
  { return name; }
}

